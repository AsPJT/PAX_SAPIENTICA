cmake_minimum_required(VERSION 3.16)
project(PAX_SAPIENTICA)
include(CTest)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

# Add custom CMake modules path
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")

if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release)
endif()

if(MSVC)
    if(CMAKE_CXX_FLAGS MATCHES "/W[0-4]")
        string(REGEX REPLACE "/W[0-4]" "/W4" CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS}")
    else()
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W4")
    endif()
    if (CMAKE_BUILD_TYPE STREQUAL "Debug")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /Od")
    elseif (CMAKE_BUILD_TYPE STREQUAL "Release")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /O2")
    endif()
elseif(CMAKE_COMPILER_IS_GNUCC OR CMAKE_COMPILER_IS_GNUCXX)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wno-long-long -pedantic")
    if (CMAKE_BUILD_TYPE STREQUAL "Debug")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -O0")
    elseif (CMAKE_BUILD_TYPE STREQUAL "Release")
        set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -O3")
    endif()
endif()

# Determine OS-specific directory name
if(APPLE)
    set(OS "macOS")
elseif(UNIX)
    set(OS "Ubuntu")
elseif(WIN32)
    set(OS "Windows")
else()
    message(FATAL_ERROR "Unsupported OS")
endif()

# Find SFML
if(WIN32 OR APPLE)
    find_package(SFMLCustom REQUIRED)
elseif(UNIX)
    find_package(SFML COMPONENTS Graphics QUIET)
    if(NOT SFML_FOUND)
        message(STATUS "SFML 3.0 not found on Linux - MapViewer will not be built")
    endif()
endif()

if(SFML_FOUND)
    if(SFML_INCLUDE_DIRS)
        include_directories(${SFML_INCLUDE_DIRS})
    endif()
else()
    message(WARNING "SFML not found. SFML-based targets will not be built.")
endif()

# Find DxLib (Windows only)
if(WIN32)
    find_package(DxLib)
    if(DxLib_FOUND)
        message(STATUS "DxLib found: ${DxLib_INCLUDE_DIRS}")
    else()
        message(STATUS "DxLib not found. Set DXLIB_ROOT_DIR to enable DxLib support.")
    endif()
endif()

# Find Siv3D (Windows only for now)
if(WIN32)
    find_package(Siv3D)
    if(Siv3D_FOUND)
        message(STATUS "Siv3D found: ${Siv3D_INCLUDE_DIRS}")
    else()
        message(STATUS "Siv3D not found. Set SIV3D_ROOT_DIR or SIV3D_0_6_13 environment variable to enable Siv3D support.")
    endif()
endif()

add_subdirectory(IncludeTest)
add_subdirectory(IntegrationTest)
add_subdirectory(MapViewer)
add_subdirectory(UnitTest)
